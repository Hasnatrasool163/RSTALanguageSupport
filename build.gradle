plugins {
    id 'java-library'
}

['java', 'distribution', 'maven-publish', 'signing'].each { apply plugin: it }

assert org.gradle.api.JavaVersion.current().isJava8Compatible()
allprojects {
	tasks.withType(Javadoc) {
		options.addStringOption('Xdoclint:none', '-quiet')
	}
}

group = 'com.fifesoft'
archivesBaseName = 'languagesupport'
ext.isReleaseVersion = !project.version.endsWith('SNAPSHOT')

dependencies {
    api 'com.fifesoft:rsyntaxtextarea:3.0.8'
    api 'com.fifesoft:autocomplete:3.0.5'
	implementation 'org.mozilla:rhino:1.7.6'
    testImplementation 'junit:junit:4.12'
}

wrapper {
    gradleVersion = '6.1'
}

repositories {
    mavenCentral()
}

compileJava {
	sourceCompatibility javaVersion
	targetCompatibility javaVersion
	options.debug = true
	options.debugOptions.debugLevel = 'source,vars,lines'
	options.compilerArgs << '-Xlint:deprecation' << '-Xlint:unchecked'
}

ext.sharedManifest = manifest {
	attributes('Main-Class': 'org.fife.rsta.ac.demo.DemoApp',
		'Class-Path': 'rsyntaxtextarea.jar autocomplete.jar rhino-1.7.6.jar',
		'Specification-Title': 'RSyntaxTextArea Language Support',
		'Specification-Version': version,
		'Implementation-Title': 'org.fife.ui',
		'Implementation-Version': version)

}
jar {
	manifest { from sharedManifest }
}

// We use "distributions" to create the zip files uploaded to SourceForge
distributions {
	main {
        distributionBaseName = 'language_support'
		contents {
			from { [ 'build/libs', // The built jar
				     configurations.runtimeClasspath, // all runtime dependencies (e.g. rhino)
				     '../RSyntaxTextArea/RSyntaxTextArea/build/libs/rsyntaxtextarea-3.0.0-SNAPSHOT.jar',
					 '../AutoComplete/AutoComplete/build/libs/autocomplete-3.0.0-SNAPSHOT.jar',
				     'RSTALanguageSupport.License.txt', 'readme.txt' ] }
			rename 'RSyntaxTextArea-.*\\.jar', 'rsyntaxtextarea.jar'
			rename 'AutoComplete-.*\\.jar', 'autocomplete.jar'
			rename 'languagesupport-.*\\.jar', 'language_support.jar'
		}
	}
	src {
        distributionBaseName = 'language_support'
	}
}
distZip.classifier = null
distZip.dependsOn jar
srcDistZip.classifier = 'src'
srcDistZip {
	from projectDir
	include 'src/**/*'
	include 'data/**/*'
	include 'build.gradle'
	include '.classpath'
	include '.project'
	include 'gradle.properties'
	include 'gradlew*'
	include 'gradle/**/*'
	include 'README.*'
	include 'readme*'
	include '.settings/**'
}

task buildSourceForgeZips {
    doLast {
        println 'Building zip files for SourceForge'
    }
}
buildSourceForgeZips.dependsOn clean, jar, distZip, srcDistZip

publishing {
    repositories {
        maven {
            def releasesRepoUrl = 'https://oss.sonatype.org/service/local/staging/deploy/maven2/'
            def snapshotsRepoUrl = 'https://oss.sonatype.org/content/repositories/snapshots/'
            url = isReleaseVersion ? releasesRepoUrl : snapshotsRepoUrl
            credentials { // Credentials usually kept in user's .gradle/gradle.properties
                // We must defensively check for these properties so Travis CI build works
                username = project.hasProperty('ossrhUsername') ? ossrhUsername : 'unknown'
                password = project.hasProperty('ossrhPassword') ? ossrhPassword : 'unknown'
            }
        }
    }
    publications {
        maven(MavenPublication) {

            groupId = 'com.fifesoft'
            artifactId = 'languagesupport'
            version = version

            from components.java

            pom {
                name = 'LanguageSupport'
                description = 'A library adding code completion and other advanced features for Java, JavaScript, Perl, and other languages to RSyntaxTextArea.'
                url = 'http://www.fifesoft.com/rsyntaxtextarea/'
                inceptionYear = '2003'
                packaging = 'jar'
                licenses {
                    license {
                        name = 'BSD-3-Clause'
                        url = 'https://github.com/bobbylight/RSTALanguageSupport/blob/master/RSTALanguageSupport.License.txt'
                    }
                }
                developers {
                    developer {
                        name = 'Robert Futrell'
                    }
                }
                scm {
                    url = 'https://github.com/bobbylight/RSTALanguageSupport'
                    connection = 'scm:git:git://github.com/bobbylight/RSTALanguageSupport'
                    developerConnection = 'scm:git:git@github.com:bobbylight/RSTALanguageSupport'
                    if (isReleaseVersion) {
                        tag = project.version
                    }
                }
            }
        }
    }
}

signing {
    // Don't require signing for e.g. ./gradlew install
    required { gradle.taskGraph.hasTask('publish') && isReleaseVersion }
    sign publishing.publications.maven
}
tasks.withType(Sign) {
    onlyIf { isReleaseVersion }
}
